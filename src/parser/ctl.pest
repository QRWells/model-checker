WHITESPACE = _{ " " | "\t" }

formulae = { SOI ~ formula ~ EOI }
formula  = { prefix* ~ primary ~ (infix ~ prefix* ~ primary)* }

primary = _{ TRUE | AP | "(" ~ formula ~ ")" }
infix   = _{ And | Or | Until | Release | WeakUntil | Implies }
prefix  = _{ All | Exists | Not | Next | Finally | Globally }

And       = @{ "/\\" | "&&" }
Or        = @{ "\\/" | "||" }
Implies   = @{ "->" | "=>" }
Not       = @{ "~" | "!" }
All       = @{ "A" }
Exists    = @{ "E" }
Next      = @{ "X" | "()" }
Finally   = @{ "F" }
Globally  = @{ "G" }
Until     = @{ "U" }
Release   = @{ "R" }
WeakUntil = @{ "W" }

TRUE = @{ "true" | "T" }
AP   = @{ ASCII_ALPHA_LOWER+ }
